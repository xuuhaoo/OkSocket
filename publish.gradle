apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

//./gradlew clean ./gradlew build ./gradlew bintrayUpload

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            groupId project.GROUP_ID
            artifactId project.ARTIFACT_ID
            version project.VERSION

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

group = project.GROUP_ID
version = project.VERSION

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def bintrayUser = properties.getProperty('bintrayUser')
    def bintrayApiKey = properties.getProperty('bintrayApiKey')

    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = "maven"
        name = project.NAME
        licenses = ["Apache-2.0"]
        publish = true
        description = "Android socket library"
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/xuuhaoo/OkSocket.git'
        dryRun = false
        version {
            name = project.VERSION
            desc = "Android socket library"
            released = new Date()
            vcsTag = project.VERSION
        }
    }
}
